buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.13'
    }
}

subprojects {
    if (!plugins.hasPlugin(JavaPlugin)) return      // only apply to project has java plugin

    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: com.github.spotbugs.snom.SpotBugsPlugin
    apply plugin: 'jacoco-report-aggregation'

    checkstyle {
        dependencies {
            checkstyle 'com.puppycrawl.tools:checkstyle:10.6.0'
            checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.44.0'
        }

        configFile = rootProject.file('gradle/check/checkstyle.xml')
        configProperties['configDir'] = configFile.parentFile

        tasks.named('checkstyleMain').configure {
            group = 'verification'
            source = fileTree(projectDir) {
                include 'conf/**/*.properties'
                include 'src/main/java/**/*.java'
                include 'src/main/**/*.properties'
            }
        }

        tasks.named('checkstyleTest').configure {
            group = 'verification'
            source = fileTree(projectDir) {
                include 'src/test/java/**/*.java'       // not include java files in resources
                include 'src/test/**/*.properties'
            }
        }
    }

    pmd {
        ruleSets = []
        ruleSetFiles = rootProject.files('gradle/check/pmd.xml')
        toolVersion = '6.53.0'
        consoleOutput = true

        tasks.withType(Pmd).configureEach {
            group = 'verification'
        }
    }

    spotbugs {
        dependencies {
            spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'
        }

        toolVersion = '4.7.3'
        reportLevel = 'low'
        extraArgs = ['-longBugCodes']
        includeFilter = rootProject.file('gradle/check/spotbugs.xml')

        def spotbugsReport = tasks.register('spotbugsReport') {
            group = 'verification'
            doLast {
                def report = file("${buildDir}/reports/spotbugs/main.txt")
                if (report.exists()) logger.error(report.text)
                report = file("${buildDir}/reports/spotbugs/test.txt")
                if (report.exists()) logger.error(report.text)
            }
        }

        tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach { task ->
            group = 'verification'
            reports.configure({
                text.enabled = true
            })
            task.finalizedBy(spotbugsReport)
        }
    }

    jacoco {
        toolVersion = '0.8.8'

        tasks.named('testCodeCoverageReport').configure {
            reports {
                xml.required = true
                xml.destination rootProject.file("${buildDir}/reports/jacoco/report.xml")
                html.required = true
                csv.required = false
            }
        }
    }
}
